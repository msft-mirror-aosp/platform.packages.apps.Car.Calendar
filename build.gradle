/*
 * Copyright (C) 2023 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'
apply plugin: 'jacoco'

android {
    namespace = "com.android.car.calendar"

    compileSdkVersion gradle.ext.aaosLatestSDK

    defaultConfig {
        applicationId 'com.android.car.calendar'
        minSdkVersion 28
        targetSdkVersion gradle.ext.aaosTargetSDK
        testInstrumentationRunnerArguments clearPackageData: 'true', coverage: 'true'
        versionCode gradle.ext.getVersionCode()
        versionName gradle.ext.getVersionName()
    }

    signingConfigs {
        aaos {
            storeFile file(gradle.ext.aaosCertPath)
            storePassword 'carapps'
            keyAlias 'carapps'
            keyPassword 'carapps'
        }

        platform {
            storeFile file(gradle.ext.platformCertPath)
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }

    flavorDimensions 'devMode'
    productFlavors {
        prod {
            dimension 'devMode'
            signingConfig signingConfigs.aaos
        }

        platform {
            dimension 'devMode'
            signingConfig signingConfigs.platform
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
        }

        androidTest.setRoot('tests')
        androidTest.java.srcDirs = ['tests/unit/src', 'tests/ui/src']
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        animationsDisabled = true
    }

    buildTypes {
        debug {
            testCoverageEnabled = true
            signingConfig android.buildTypes.release.signingConfig
        }
        release {
            minifyEnabled false
        }
    }

    // Disable release build for fake and emulator build
    variantFilter { variant ->
        def names = variant.flavors*.name
        if (variant.buildType.name == 'release') {
            if (names.contains('fake') || names.contains('emulator')) {
                setIgnore(true)
            }
        }
    }

    lintOptions {
        // Ignore duplicate resources due to existence of overlayable.xml
        ignore 'DuplicateDefinition'
        // Ignore protected permissions since Dialer is system app
        ignore 'ProtectedPermissions'
        // Ignore ImageView using android:tint
        ignore 'UseAppTint'
        // Ignore quantity errors for translations
        ignore 'MissingQuantity'
        ignore 'ImpliedQuantity'
        // Ignore missing translations
        ignore 'MissingTranslation'

        // Ignore error when a translated string/plural is removed
        ignore 'MissingDefaultResource'

        abortOnError false
    }

    useLibrary 'android.test.mock'
}

dependencies {
    implementation files(gradle.ext.lib_car_system_stubs)

    implementation project(':car-ui-lib')

    def appcompat_version = "1.6.1"
    implementation "androidx.appcompat:appcompat:$appcompat_version"
    def lifecycle_version = '2.2.0'
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    // Not available in 2.3+
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata:$lifecycle_version"

    implementation 'com.google.guava:guava:30.1.1-android'

    //Test libs
    androidTestImplementation project(':car-ui-lib-testing')
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test:core:1.5.0'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.5.1'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.5.1'
    androidTestImplementation 'com.google.truth:truth:1.1.3'
    androidTestImplementation 'com.linkedin.dexmaker:dexmaker-mockito-inline-extended:2.28.1'
    androidTestImplementation 'org.hamcrest:hamcrest:2.2'
}
